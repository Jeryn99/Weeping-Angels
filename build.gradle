plugins {
    id "architectury-plugin" version "3.4+"
    id "dev.architectury.loom" version "1.7+" apply false
    id "com.github.breadmoirai.github-release" version "2.4.1"
}

architectury {
    minecraft = rootProject.minecraft_version
}

subprojects {
    apply plugin: "dev.architectury.loom"

    loom {
        silentMojangMappingsLicense()
    }

    dependencies {
        minecraft "com.mojang:minecraft:${rootProject.minecraft_version}"
        mappings loom.layered() {
            officialMojangMappings()
            //parchment("org.parchmentmc.data:parchment-${rootProject.mappings}")
        }
    }
}

task publishMeEverywhere(){
    dependsOn ':forge:publishUnified'
    dependsOn ':fabric:publishUnified'
}


def forgeDir = new File(project.projectDir, 'forge/build/libs/')
def fabricDir = new File(project.projectDir, 'fabric/build/libs/')
def commonDir = new File(project.projectDir, 'common/build/libs/')

forgeDir.mkdirs()
fabricDir.mkdirs()
commonDir.mkdirs()

def filter = { File file -> file.getName().contains(version) } as FileFilter

def forgeFiles = forgeDir.listFiles(filter)
def fabricFiles = fabricDir.listFiles(filter)
def commonFiles = commonDir.listFiles(filter)

githubRelease {
    token project.findProperty("github") ?: System.getenv("github") ?: ""
    owner "Jeryn99"
    repo "weeping_angels"
    tagName "v$rootProject.minecraft_version-${version}"
    targetCommitish "1.20"
    releaseName "[$rootProject.minecraft_version] Weeping Angels - v$version"
    generateReleaseNotes true
    body new File("${rootProject.projectDir}/changelog.html").text
    draft false
    prerelease false
    releaseAssets(forgeFiles + fabricFiles + commonFiles)
    allowUploadToExisting.set false
    overwrite true
    dryRun false
    apiEndpoint "https://api.github.com"
    client
}


allprojects {
    apply plugin: "java"
    apply plugin: "architectury-plugin"
    apply plugin: "maven-publish"

    archivesBaseName = rootProject.archives_base_name
    version = rootProject.mod_version
    group = rootProject.maven_group

    repositories {
        maven {
            url = 'https://maven.parchmentmc.net/'
            content {
                includeGroup 'org.parchmentmc.data'
            }
        }

        maven {
            name = "Fuzs Mod Resources"
            url = "https://raw.githubusercontent.com/Fuzss/modresources/main/maven/"
        }

        maven { url "https://cursemaven.com" }

    }

    tasks.withType(JavaCompile) {
        options.encoding = "UTF-8"
        options.release = 21
    }

    java {
        withSourcesJar()
    }
}